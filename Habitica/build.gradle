apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'
apply plugin: 'aar-link-sources'
apply plugin: 'io.fabric'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven { url 'https://maven.fabric.io/public' }

    // Material View Pager
    maven {
        url "http://dl.bintray.com/florent37/maven"
    }

    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup.retrofit:retrofit:1.6.0'

    compile('com.crashlytics.sdk.android:crashlytics:2.3.0@aar') {
        transitive = true;
    }

    // View Elements Injection :)
    compile 'com.jakewharton:butterknife:6.1.0'

    compile('com.mikepenz:materialdrawer:3.0.8@aar') {
        transitive = true
    }

    compile('com.github.florent37:materialviewpager:1.0.7@aar') {
        transitive = true
    }

    compile 'com.android.support:appcompat-v7:22.2.+'
    compile 'com.android.support:design:22.2.+'
    compile 'com.android.support:gridlayout-v7:22.2.+'
    compile 'com.android.support:recyclerview-v7:22.2.+'

    // Icons
    dependencies {
        compile 'com.mikepenz:iconics:1.1.0@aar'
    }

    compile 'com.rengwuxian.materialedittext:library:2.1.4'

    // Instabug / In-App-Feedback
    compile 'com.instabug.library:instabugsupport:+'

    // Changelog Fragment, minSDK 17
    //compile 'com.github.porokoro.paperboy:paperboy:1.0.0'
    compile 'com.github.porokoro:paperboy:364c77b49f'

    // About View for all dependent Libraries, where are using
    compile('com.mikepenz:aboutlibraries:5.0.5@aar') {
        transitive = true
    }

    provided 'com.raizlabs.android:DBFlow-Compiler:2.2.1'
    compile "com.raizlabs.android:DBFlow-Core:2.2.1"
    compile "com.raizlabs.android:DBFlow:2.2.1"

    compile 'de.greenrobot:eventbus:2.4.0'
}

android {
    compileSdkVersion 22
    //buildToolsVersion "23.0.0-rc2"
    buildToolsVersion "22.0.1"

    lintOptions {
        abortOnError false
    }
    signingConfigs {
        release
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        release.setRoot('build-types/release')
        instrumentTest.setRoot('tests')
    }
}

def Properties props = new Properties()
def propFile = new File('signingrelease.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}
